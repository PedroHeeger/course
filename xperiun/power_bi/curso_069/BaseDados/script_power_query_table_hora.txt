let
    //Quantidade de segundos em um dia (60*60*24 = 86.400), logo de 0 à 86.399
    Lista = {0..86399},
    ConverteParaTabela = Table.FromList(Lista, Splitter.SplitByNothing(), null, null, ExtraValues.Error),

    //Alterando o tipo e o nome da coluna
    AlteraTipo = Table.TransformColumnTypes(ConverteParaTabela, {{"Column1", Int64.Type}}),
    AlteraNomeCol = Table.RenameColumns(AlteraTipo, {{"Column1", "Segundo"}}),

    //Calculando o tempo de duração em segundos
    ColDuracao = Table.AddColumn(AlteraNomeCol, "Duração", each #duration(0,0,0, [Segundo]), type duration),
    
    //Transformando a coluna de duração em uma coluna de horário (com hora, minuto e segundo definido)
    ColHora = Table.AddColumn(ColDuracao, "Hora", each #time(Duration.Hours([Duração]), Duration.Minutes([Duração]), Duration.Seconds([Duração])), type text),
    RemoveCols = Table.RemoveColumns(ColHora, {"Duração", "Segundo"}),

    //Separando as colunas de hora, minuto e segundo
    ColHoraNum = Table.AddColumn(RemoveCols, "Hora Num", each Time.Hour([Hora]), Int64.Type),
    ColMinutoNum = Table.AddColumn(ColHoraNum, "Minuto Num", each Time.Minute([Hora]), Int64.Type),
    ColSegundoNum = Table.AddColumn(ColMinutoNum, "Segundo Num", each Time.Second([Hora]), Int64.Type),

    //Adicionando de turno (AM ou PM)
    ColAMPM = Table.AddColumn(ColSegundoNum, "AMPM", each if [Hora Num] < 12 then "AM" else "PM", type text),

    //Adicionando a coluna de Hora com turno (12 AM)
    ColHoraAMPM = Table.AddColumn(ColAMPM, "Hora AMPM", each
        if [Hora Num] = 0 then "12 AM"
        else if [Hora Num] >= 1 and [Hora Num] <= 11 then Number.ToText([Hora Num]) & " AM"
        else if [Hora Num] = 12 then "12 PM"
        else if [Hora Num] >= 13 then Number.ToText([Hora Num] - 12) & " PM"
        else "Unknown", type text),
    
    //Adicionando a coluna de Período
    ColPeriodo = Table.AddColumn(ColHoraAMPM, "Período", each 
        if [Hora Num] >= 0 and [Hora Num] <= 5 then "12AM - 6AM"
        else if [Hora Num] >= 6 and [Hora Num] <= 11 then "6AM - 12PM"
        else if [Hora Num] >= 12 and [Hora Num] <= 17 then "12PM - 6PM"
        else if [Hora Num] >= 18 and [Hora Num] <= 23 then "6PM - 12AM"
        else "Unknown", type text),

    //Adicionando a coluna de Ordem de Período
    ColPeriodoOrdem = Table.AddColumn(ColPeriodo, "Período Ordem", each
        if [Hora Num] >= 0 and [Hora Num] <= 5 then 1
        else if [Hora Num] >= 6 and [Hora Num] <= 11 then 2
        else if [Hora Num] >= 12 and [Hora Num] <= 17 then 3
        else if [Hora Num] >= 18 and [Hora Num] <= 23 then 4
        else 5, Int64.Type
    ),

    //Adicionando a coluna de Faixa Hora (02h, 18h)
    FaixaHora = Table.AddColumn(ColPeriodoOrdem, "Faixa Hora", each Text.Combine({Text.Start(Text.From([Hora], "pt-BR"), 2), "h"}), type text)
    
in
    FaixaHora